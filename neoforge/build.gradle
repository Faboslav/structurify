plugins {
	id 'java'
	id 'idea'
	id 'dev.architectury.loom'
	id 'architectury-plugin'
	id 'com.github.johnrengelman.shadow'
}

def currentRootVersion = getRootProject().stonecutter.current.version;
def subproject = stonecutter.current.project
def minecraftVersion = stonecutter.current.version

def modrinth(String name, String version) {
	return "maven.modrinth:$name:$version"
}

def prop(String name) {
	return rootProject.project(stonecutter.current.project).property(name).toString()
}

base {
	archivesName.set("${mod_id}-neoforge-mc${minecraftVersion}-${mod_version}")
}

architectury {
	platformSetupLoomIde()
	neoForge()
}

loom {
	silentMojangMappingsLicense()

	accessWidenerPath.set(project(":common:$subproject").loom.accessWidenerPath)

	runConfigs.all {
		ideConfigGenerated true
	}
}

configurations {
	common {
		canBeResolved = true
		canBeConsumed = false
	}

	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentNeoForge.extendsFrom common

	shadowBundle {
		canBeResolved = true
		canBeConsumed = false
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraftVersion}"
	mappings loom.officialMojangMappings()

	// Core
	neoForge "net.neoforged:neoforge:${prop('neoforge_version')}"

	// Mixin extras (https://github.com/LlamaLad7/MixinExtras)
	implementation(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixin_extras_version}"))
	implementation(include("io.github.llamalad7:mixinextras-neoforge:${mixin_extras_version}"))

	// Dependencies
	modImplementation(modrinth("yacl", "${prop('yacl_version')}-neoforge"))

	// For YACL to work
	forgeRuntimeLibrary(implementation(include("com.twelvemonkeys.imageio:imageio-core:3.10.0")))
	forgeRuntimeLibrary(implementation(include("com.twelvemonkeys.imageio:imageio-webp:3.10.0")))
	forgeRuntimeLibrary(implementation(include("com.twelvemonkeys.imageio:imageio-metadata:3.10.0")))
	forgeRuntimeLibrary(implementation(include("com.twelvemonkeys.common:common-lang:3.10.0")))
	forgeRuntimeLibrary(implementation(include("com.twelvemonkeys.common:common-io:3.10.0")))
	forgeRuntimeLibrary(implementation(include("com.twelvemonkeys.common:common-image:3.10.0")))
	forgeRuntimeLibrary(implementation(include("org.quiltmc.parsers:json:0.2.1")))
	forgeRuntimeLibrary(implementation(include("org.quiltmc.parsers:gson:0.2.1")))


	// Compat
	modImplementation(modrinth("repurposed-structures-forge", "${prop('repurposed_structures_version')}-neoforge"))

	def availableVersions = stonecutter_enabled_neoforge_versions.split(',').collect { it.trim() }

	if (!availableVersions.contains(currentRootVersion) && minecraftVersion != currentRootVersion) {
		common(project(path: ":common:$currentRootVersion", configuration: 'namedElements')) { transitive false }
		shadowBundle(project(path: ":common:$subproject", configuration: 'transformProductionNeoForge')) { transitive false }
	} else {
		common(project(path: ":common:$subproject", configuration: 'namedElements')) { transitive false }
		shadowBundle(project(path: ":common:$subproject", configuration: 'transformProductionNeoForge')) { transitive false }
	}
}

processResources {
	def expandProps = [
		"modJavaVersion"     : prop('mod_java_version'),
		'modId'              : mod_id,
		'modName'            : mod_name,
		'modDescription'     : mod_description,
		'modVersion'         : mod_version,
		'modAuthor'          : mod_author,
		"minecraftVersion"   : prop('minecraft_version'),
		"minMinecraftVersion": prop('min_minecraft_version'),
		"neoForgeVersion"    : prop('neoforge_version'),
		"yaclVersion"        : prop('yacl_version'),
	]

	def tomlFileName = "META-INF/mods.toml"

	if (stonecutter.compare(minecraftVersion, '1.20.6') >= 0) {
		tomlFileName = "META-INF/neoforge.mods.toml"
	}

	filesMatching(['pack.mcmeta', tomlFileName, '*.mixins.json']) {
		expand expandProps
	}

	inputs.properties(expandProps)
}

shadowJar {
	exclude("fabric.mod.json")
	exclude("architectury.common.json")

	configurations = [project.configurations.shadowBundle]
	archiveClassifier = 'dev-shadow'
}

remapJar {
	injectAccessWidener.set(true)
	input.set(shadowJar.archiveFile)
	dependsOn(shadowJar)
	archiveClassifier.set(null)
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.toVersion(prop('mod_java_version'))
	targetCompatibility = JavaVersion.toVersion(prop('mod_java_version'))
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.getRelease().set(prop('mod_java_version') as Integer)
}

repositories {
	// CurseForge
	maven { url "https://www.cursemaven.com" }

	// Modrinth
	maven {
		url "https://api.modrinth.com/maven"
		content {
			includeGroup("maven.modrinth")
		}
	}

	// NeoForge
	maven { url 'https://maven.neoforged.net/releases/' }

	// Quilt
	maven { url "https://maven.quiltmc.org/repository/release" }

	// Mixin Extras.
	maven {
		url 'https://jitpack.io'
		content {
			includeGroup 'com.github.llamalad7'
		}
	}

	// YACL
	maven { url "https://maven.isxander.dev/releases" }
	maven { url "https://maven.isxander.dev/snapshots" }
	maven { url 'https://thedarkcolour.github.io/KotlinForForge/' }

	// Mod Menu.
	maven { url "https://maven.terraformersmc.com/releases" }
	maven { url "https://maven.terraformersmc.com" }

	// Random
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

if (stonecutter.current.isActive) {
	rootProject.tasks.register("buildActiveNeoForge") {
		group = "project"
		dependsOn(tasks.named("build"))
	}
}